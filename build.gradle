plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'no.unit.nva'
version '1.0-SNAPSHOT'

sourceCompatibility =  JavaVersion.VERSION_11  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_11 // bytecode target version

allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

dependencies {
    implementation group: 'com.github.BIBSYSDEV', name: 'nva-commons', version: 'v0.3.2'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    implementation group: 'javax.ws.rs', name: 'javax.ws.rs-api', version: '2.1.1'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.29'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.12.1'
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.0'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: '1.11.715'
    implementation group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.22.2'
    implementation group: 'org.zalando', name: 'problem', version: '0.23.0'
    implementation group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.23.0'

    testImplementation group: 'org.glassfish.jersey.core', name: 'jersey-common', version: '2.22.2'
    testImplementation group: 'com.github.BIBSYSDEV', name: 'nva-testutils', version: 'v0.1.6'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.6'
}

test {
    failFast = true
    finalizedBy jacocoTestReport
}

task buildZip(type: Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
}

project.tasks.build.dependsOn project.tasks.buildZip


// We don't want a jar, just a fatJar
//jar.enabled = false

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}
